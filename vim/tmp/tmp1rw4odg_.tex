
% Input your problem and solution below.
% Three dashes on a newline indicate the breaking points.
% vim: tw=72

---

Let $ABC$ be a triangle with incenter $I$, and let $D$ be a point on line $BC$ satisfying $\angle AID=90^\circ$. Denote by $E$ and $F$ the feet of the altitudes from $B$ and $C$, respectively.

Let the excircle of triangle $ABC$ opposite the vertex $A$ be tangent to $\overline{BC}$ at point $A_1$. Define points $B_1$ on $\overline{CA}$ and $C_1$ on $\overline{AB}$ analogously, using the excircles opposite $B$ and $C$, respectively. Prove that:
\begin{itemize}
    \item[(a)] (MOP 2019) If line $EF$ is tangent to the incircle of $\triangle ABC$, then quadrilateral $AB_1A_1C_1$ is cyclic.
        \vspace{-0.5em}
    \item[(b)] (USA TST 2019/6) If quadrilateral $AB_1A_1C_1$ is cyclic, then $\overline{AD}$ is tangent to the circumcircle of $\triangle DB_1C_1$.
\end{itemize}

---

\begin{customsol}{to part (a)}\
    \begin{center}
        \begin{asy}
            size(12cm);
            defaultpen(fontsize(10pt));

            pen pri=purple;
            pen sec=magenta;
            pen tri=fuchsia;
            pen qua=red;
            pen fil=pri+opacity(0.05);
            pen qfil=qua+opacity(0.05);

            pair B, C, L, I, A, A0, B0, C0, EE, F, A1, B1, C1, T, X, H;
            B=dir(190); C=dir(350); L=dir(270);
            I=intersectionpoint(arc(L, length(B-L), 90, 180, CCW), (-B) -- (-C));
            A=intersectionpoint(I -- (I+100*(I-L)), circle((0, 0), 1));
            A0=foot(I, B, C);
            B0=foot(I, C, A);
            C0=foot(I, A, B);
            EE=foot(B, C, A);
            F=foot(C, A, B);
            A1=B+C-A0;
            B1=foot(A1, A, C);
            C1=foot(A1, A, B);
            T=foot(I, EE, F);
            X=foot(A, B, C);
            H=orthocenter(A, B, C);

            filldraw(A -- B -- C -- cycle, fil, pri);
            filldraw(incircle(A, B, C), fil, pri);
            draw(B -- EE, sec+dashed); draw(C -- F, sec+dashed);
            draw(A -- X, sec);
            draw(B1 -- A1 -- C1 -- B1, sec);
            draw(A0 -- I -- T, tri); draw(B0 -- I -- C0 -- B0, tri);
            draw(EE -- F, pri);
            draw(T -- A0, qua);
            filldraw(circumcircle(A, B1, C1), qfil, qua);

            dot("$A$", A, N);
            dot("$B$", B, SW);
            dot("$C$", C, SE);
            dot("$A_0$", A0, S);
            dot("$B_0$", B0, unit(B0-I));
            dot("$C_0$", C0, dir(140));
            dot("$A_1$", A1, SE);
            dot("$B_1$", B1, dir(30));
            dot("$C_1$", C1, W);
            dot("$E$", EE, unit(EE-B));
            dot("$F$", F, dir(150));
            dot("$T$", T, dir(80));
            dot("$X$", X, SW);
            dot("$H$", H, dir(320));
            dot("$I$", I, N);
        \end{asy}
    \end{center}
    Denote the intouch points by $A_0,B_0,C_0$, the orthocenter by $H$, the $A$-excenter by $I_A$, the Bevan point\footnote{the circumcenter of the excentral triangle} by $V$, and the point where the incircle touches $\overline{EF}$ by $T$. By Brianchon's Theorem on $BCB_0EFC_0$ and $BA_0CETF$, $\overline{B_0C_0}$ and $\overline{A_0T}$ intersect at $H$. Remark that $\overline{AH}\parallel\overline{IA_0}$. Since $BCEF$ is both cyclic and tangential, \[\angle A_0HB_0=\frac{\widehat{A_0B_0}+\widehat{TC_0}}2=\frac{(180^\circ-\angle B_0CA_0)+(180^\circ-\angle C_0FT)}2=90^\circ,\]
    whence $\overline{HA_0}\perp\overline{B_0C_0}$. However, $\overline{AI}\perp\overline{B_0C_0}$, so $\overline{AI}\parallel\overline{HA_0}$, and $AHA_0I$ is a parallelogram.

    Let $R,r,r_A$ denote the circumradius, the inradius, and the $A$-exradius, respectively. Note that $2R\cos A=AH=IA_0=r$. It is easy to see that $\triangle ABC\sim\triangle AEF$ with scale factor $\cos A$, and also $r$ is the $A$-exradius of $\triangle AEF$, so $2R=r_A$. This implies that $I_AV=2R=r_A=I_AA_1$. Since the excentral triangle is acute, $A_1=V$, whence $\angle AB_1A_1=\angle AC_1A_1=90^\circ$, and we are done. 
\end{customsol}
\begin{customenv}{First solution to part (b)}
    Let $V$ be the antipode of $A$ on $(AB_1C_1)$. Let the incircle of $\triangle ABC$ touch $\overline{CA}$ and $\overline{AB}$ at $E$ and $F$, respectively, and let $J$ be the Miquel point of $BCEF$. Furthermore, let $M$ be the midpoint of $\overline{AD}$.

    We claim that $\overline{AA_1}$ is a diamter of $(AB_1A_1C_1)$. Note that $V$ is the Bevan point of $\triangle ABC$, so $\overline{VA_1}\perp\overline{BC}$. Furthermore, if $V\ne A_1$, then $\overline{VA_1}\perp\overline{AA_1}$, which would require that $A\in\overline{BC}$, which is absurd.
    \begin{center}
        \begin{asy}
            size(12cm);
            defaultpen(fontsize(10pt));

            pen pri=royalblue+linewidth(0.5);
            pen sec=Cyan+linewidth(0.5);
            pen tri=springgreen+linewidth(0.5);
            pen qua=chartreuse+linewidth(0.5);
            pen fil=pri+opacity(0.05);
            pen sfil=sec+opacity(0.05);
            pen tfil=tri+opacity(0.05);
            pen qfil=qua+opacity(0.05);

            pair B, C, L, I, A, A1, B1, C1, EE, F, J, D, M, K, IB, IC;
            B=dir(190); C=dir(350); L=dir(270);
            I=intersectionpoint(arc(L, length(B-L), 90, 180, CCW), (-B) -- (-C));
            A=intersectionpoint(I -- (I+100*(I-L)), circle((0, 0), 1));
            A1=B+C-foot(I, B, C);
            B1=foot(A1, A, C);
            C1=foot(A1, A, B);
            EE=foot(I, A, C);
            F=foot(I, A, B);
            J=intersectionpoints(circumcircle(A, B, C), circumcircle(A, EE, F))[1];
            D=extension(A, J, B, C);
            M=(A+D)/2;
            K=dir(90);
            IB=extension(B, I, A, K);
            IC=extension(C, I, A, K);

            draw(A -- B -- C -- A, pri);
            filldraw(circumcircle(A, B, C), fil, pri);
            draw(IB -- A1 -- IC -- IB, sec);
            filldraw(circumcircle(A, B1, C1), tfil, tri);
            draw(B -- D -- A, pri);
            filldraw(circumcircle(A, EE, F), sfil, sec);
            filldraw(circumcircle(B, I, C), qfil, qua);
            draw(B -- IB, tri+dashed); draw(C -- IC, tri+dashed);
            draw(M -- B1, pri);
            draw(D -- I -- J, sec); draw(A -- L, pri);

            clip((L + (100, -1/8)) -- (L+(-100, -1/8)) -- (-100, 100) -- (100, 100) -- cycle);

            dot("$A$", A, NW);
            dot("$B$", B, SW);
            dot("$C$", C, SE);
            dot("$L$", L, S);
            dot("$I$", I, NE);
            dot("$A_1$", A1, SE);
            dot("$B_1$", B1, NE);
            dot("$C_1$", C1, SW);
            dot("$E$", EE, NE);
            dot("$F$", F, NW);
            dot("$J$", J, dir(150));
            dot("$D$", D, SW);
            dot("$M$", M, dir(195));
            dot("$K$", K, N);
            dot("$I_B$", IB, NE);
            dot("$I_C$", IC, W);
        \end{asy}
    \end{center}
    By Pappus' Theorem on $\overline{BA_1C}$ and $\overline{I_CAI_B}$, $I$ lies on $\overline{B_1C_1}$, and by the Radical Axis Theorem on $(AI)$, $(ABC)$, and $(BIC)$, $J$ lies on $\overline{AD}$. Since $\triangle JBF\sim\triangle JCE$, \[\frac{AC_1}{AB_1}=\frac{JF}{JE}=\frac{JB}{JC},\]
    and also $\measuredangle C_1AB_1=\measuredangle BAC=\measuredangle BJC$, so $\triangle AC_1B_1\sim\triangle JBC$. This implies that \[\measuredangle DAB=\measuredangle JAF=\measuredangle JEF=\measuredangle JCB=\measuredangle AB_1C_1,\]
    so $\overline{AD}$ is tangent to $(AB_1C_1)$. Moreover, \[\measuredangle MIA=\measuredangle IAM=\measuredangle IAC_1+\measuredangle C_1AM=\measuredangle B_1AI+\measuredangle C_1B_1A=\measuredangle B_1IA,\]
    whence $M$ lies on $\overline{B_1IC_1}$. Hence, $MD^2=MA^2=MB_1\cdot MC_1$, and the desired result follows. 
\end{customenv}
\begin{customenv}{Second solution to part (b), by harmonic bundles}
    Like above, check that $\overline{AA_1}$ is a diameter of $(AB_1C_1)$ and that $I$ lies on $\overline{B_1C_1}$. Denote by $X$ the foot of the altitude from $A$. Notice that \[-1=(A,\overline{BC}\cap\overline{I_BI_C};I_B,I_C)\stackrel{A_1}=(AX;B_1C_1).\]
    Let the tangents to $(AB_1C_1)$ at $A$ and $X$ meet at $M$. Since $I$ is the foot of the $A$-angle bisector of $\triangle AB_1C_1$ and $AB_1XC_1$ is harmonic, $(AIX)$ is the $A$-Apollonius circle of $\overline{B_1C_1}$.

    Since $\angle DIA=90^\circ=\angle DXA$, $AIXD$ is cyclic. However $M$ is the circumcenter of $\triangle AIX$, so $M$ is the midpoint of $\overline{AD}$, and $MD^2=MA^2=MB_1\cdot MC_1$. This completes the proof. 
\end{customenv}

