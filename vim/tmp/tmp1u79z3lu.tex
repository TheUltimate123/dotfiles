% Input your problem and solution below.
% Three dashes on a newline indicate the breaking points.

---

Let $ABCDEF$ be a convex hexagon with $AB=DE$, $BC=EF$, $CD=FA$, and $\angle A-\angle D=\angle C-\angle F=\angle E-\angle B$. Prove that the diagonals $AD$, $BE$, $CF$ are concurrent.

---

As shown below, let $PQR$ be the triangle formed by lines $DE$, $FA$, $BC$ and $UVW$ the triangle formed by lines $AB$, $CD$, $EF$. Also let $FABX$, $BCDY$, $DEFZ$ be parallelograms.
\begin{center}
\begin{asy}
    size(6cm); defaultpen(fontsize(10pt));
    pen pri=blue;
    pen sec=lightred;
    pen tri=purple+pink;
    pen qua=heavycyan;
    pen fil=cyan+white+white+white+white;
    pen sfil=sec+opacity(0.05);
    pen tfil=tri+opacity(0.05);
    pen qfil=springgreen+opacity(0.05);

    pair x,y,z,O,A,B,C,D,EE,F,P,Q,R,U,V,WW;
    real t=10;
    x=dir(0);
    y=0.8*dir(110);
    z=-x-y;
    O=(0,0);
    EE=(0,0);
    D=EE+x;
    C=D-rotate(t,O)*z;
    B=C+y;
    A=B-rotate(t,O)*x;
    F=A+z;
    pair X,Y,Z;
    X=B+F-A;
    Y=D+B-C;
    Z=F+D-EE;
    P=extension(F,A,B,C);
    Q=extension(B,C,D,EE);
    R=extension(D,EE,F,A);
    U=extension(C,D,EE,F);
    V=extension(EE,F,A,B);
    WW=extension(A,B,C,D);

    draw(D--U--EE,tri);
    draw(F--V--A,tri);
    draw(B--WW--C,tri);
    fill(U--V--WW--cycle,tfil);
    draw(A--P--B,sec);
    draw(C--Q--D,sec);
    draw(EE--R--F,sec);
    fill(P--Q--R--cycle,sfil);
    filldraw(A--B--C--D--EE--F--cycle,fil,pri);
    fill(A--B--X--F--cycle,qfil);
    draw(B--X--F,qua);
    fill(C--D--Y--B--cycle,qfil);
    draw(D--Y--B,qua);
    fill(EE--F--Z--D--cycle,qfil);
    draw(F--Z--D,qua);

    dot("$A$",A,NW);
    dot("$B$",B,dir(60));
    dot("$C$",C,E);
    dot("$D$",D,SE);
    dot("$E$",EE,SW);
    dot("$F$",F,W);
    dot("$P$",P,N);
    dot("$Q$",Q,SE);
    dot("$R$",R,SW);
    dot("$U$",U,S);
    dot("$V$",V,NW);
    dot("$W$",WW,NE);
    dot("$X$",X,NW);
    dot("$Y$",Y,E);
    dot("$Z$",Z,SW);
\end{asy}
\end{center}
\begin{claim*}
    $\triangle PQR\sim\triangle XYZ$.
\end{claim*}
\begin{proof}
    Check that $\angle P=\angle A+\angle B-180\dg=\angle D+\angle E-180\dg=\angle U$.
\end{proof}

Observe that $BX=AF=CD=BY$, and since $\triangle FXE\cong\triangle DEY$ by SAS, we have $EX=EY$. Then $\seg{BE}$ is the perpendicular bisector of $\seg{XY}$, so the three diagonals concur at the circumcenter of $\triangle XYZ$.
\begin{remark}
    It can be shown that $BXEY$ is a rhombus, so $CDEX$, $DFAY$, $ABCZ$ are also parallelograms.
\end{remark}

