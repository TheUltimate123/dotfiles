% Input your problem and solution below.
% Three dashes on a newline indicate the breaking points.

---

Let $ABC$ be a triangle, and $D$ be a point on the internal angle bisector of $\angle BAC$ but not on the circumcircle of $\triangle ABC$. Suppose that the circumcircle of $\triangle ABD$ intersects $\overline{AC}$ again at $P$ and the circumcircle of $\triangle ACD$ intersects $\overline{AB}$ again at $Q$. Denote by $O_1$ and $O_2$ the circumcenters of $\triangle ABD$ and $\triangle ACD$, respectively. Prove that the circumcenters of $\triangle ABC$, $\triangle APQ$, and $\triangle AO_1O_2$ are collinear.

---

\begin{center}
    \begin{asy}
        size(6cm);
        defaultpen(fontsize(10pt));

        pen pri=deepblue;
        pen sec=deepcyan;
        pen tri=Cyan;
        pen fil=pri+opacity(0.05);
        pen sfil=sec+opacity(0.05);
        pen tfil=tri+opacity(0.05);

        pair O, A, B, C, L, K, D, O1, O2, P, Q;
        O=(0, 0);
        A=dir(125);
        B=dir(205);
        C=dir(335);
        L=dir(270);
        K=dir(90);
        D=(A+4.5L)/5.5;
        O1=circumcenter(A, B, D);
        O2=circumcenter(A, C, D);
        P=intersectionpoints(circumcircle(A, D, B), A -- C)[1];
        Q=intersectionpoints(circumcircle(A, D, C), A -- B)[1];

        filldraw(circumcircle(A, B, D), sfil, sec); filldraw(circumcircle(A, C, D), sfil, sec);
        filldraw(D -- B -- Q -- cycle, sfil, sec); filldraw(D -- C -- P -- cycle, sfil, sec);
        filldraw(circumcircle(A, P, Q), tfil, tri);
        filldraw(circumcircle(A, O1, O2), tfil, tri);
        draw(K -- A -- L, pri); draw(O1 -- O2, pri);
        draw(O2 -- O -- (A+B)/2, pri);
        draw(A -- B -- C -- A, pri); filldraw(circumcircle(A, B, C), fil, pri);

        dot("$A$", A, NW);
        dot("$B$", B, SW);
        dot("$C$", C, SE);
        dot("$D$", D, dir(240));
        dot("$L$", L, S);
        dot("$K$", K, N);
        dot("$P$", P, dir(15));
        dot("$Q$", Q, W);
        dot("$O_1$", O1, dir(80));
        dot("$O_2$", O2, dir(-30));
        dot("$O$", O, S);
    \end{asy}
\end{center}
Let $K$ be the midpoint of arc $BAC$ on $(ABC)$. I claim that $(APQ)$ and $(AO_1O_2)$ pass through $K$, from which the result is obvious.
\setcounter{iclaim}0
\begin{iclaim}
    $K$ lies on $(APQ)$.
\end{iclaim}
\begin{proof}
    By construction, $D$ is the center of spiral similarity sending $\overline{BQ}$ to $\overline{PC}$. However, since $\overline{AD}$ bisects $\angle BAP$, $DB=DP$, so $\triangle DBQ\cong\triangle DPC$, and $BQ=PC$. Since $\measuredangle QBK=\measuredangle ABK=\measuredangle ACK=\measuredangle PCK$, by SAS, $\triangle KBQ\cong\triangle KCP$, so $K$ is the Miquel point of $BCPQ$, and $K$ lies on $(APQ)$, as desired.
\end{proof}
\begin{iclaim}
    $OO_1=OO_2$, where $O$ is the circumcenter of $\triangle ABC$.
\end{iclaim}
\begin{proof}
    Note that $\overline{O_1O_2},\overline{OO_1},\overline{OO_2}$ are the perpendicular bisectors of $\overline{AD},\overline{AB},\overline{AC}$, respectively, so \[\measuredangle(\overline{OO_1},\overline{O_1O_2})=\measuredangle(\overline{AB},\overline{AD})=\measuredangle(\overline{AD},\overline{AC})=\measuredangle(\overline{O_1O_2},\overline{OO_2}),\]
    as required.
\end{proof}
\begin{iclaim}
    $K$ lies on $(AO_1O_2)$.
\end{iclaim}
\begin{proof}
    Since $\overline{O_1O_2}$ and $\overline{AK}$ are both perpendicular to $\overline{AD}$, and $O$ lies on both of their perpendicular bisectors, $AO_1O_2K$ must be an isosceles trapezoid, so it is cyclic.
\end{proof}

Hence, $(ABC)$, $(APQ)$, and $(AO_1O_2)$ are coaxial, so their centers are collinear, as desired.

