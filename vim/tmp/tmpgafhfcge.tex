% Input your problem and solution below.
% Three dashes on a newline indicate the breaking points.
% vim: tw=72

---

Let $ABCD$ be a quadrilateral inscribed in circle $\omega$ with $\seg{AC}\perp\seg{BD}$. Let $E$ and $F$ be the reflections of $D$ over lines $BA$ and $BC$, respectively, and let $P$ be the intersection of lines $BD$ and $EF$. Suppose that the circumcircle of $\triangle EPD$ meets $\omega$ at $D$ and $Q$, and the circumcircle of $\triangle FPD$ meets $\omega$ at $D$ and $R$. Show that $EQ=FR$.

---

\begin{center}
    \begin{asy}
        size(10cm);
        defaultpen(fontsize(10pt));

        pen pri=Cyan;
        pen sec=deepgreen;
        pen tri=springgreen;
        pen qua=blue;
        pen qui=purple;
        pen fil=cyan+opacity(0.05);
        pen sfil=sec+opacity(0.05);
        pen tfil=tri+opacity(0.05);

        pair O, B, A, C, H, D, EE, F, P, Q, R;
        O=(0,0);
        B=dir(105);
        A=dir(200);
        C=dir(340);
        H=A+B+C;
        D=2*foot(O,B,H)-B;
        EE=reflect(B,A)*D;
        F=reflect(B,C)*D;
        P=reflect(A,C)*D;
        Q=2*foot(O,B,EE)-B;
        R=2*foot(O,B,F)-B;

        filldraw(circumcircle(EE,P,D),tfil,sec);
        filldraw(circumcircle(F,P,D),tfil,sec);
        filldraw(circle(O,1),fil,pri);
        draw(P--D,qua);
        draw(EE--Q,qua);
        draw(F--R,qua);
        draw(Q--B--R,qua+dotted);
        draw(A--R,qui+dotted);
        draw(C--Q,qui+dotted);
        draw(EE--D--F,sec+dotted);
        draw(EE--F,sec);
        draw(A--B--C--D--A--C,pri);
        draw(B--P,pri);
        draw(D--A,pri);

        dot("$A$",A,A);
        dot("$B$",B,B);
        dot("$C$",C,C);
        dot("$D$",D,1.5*dir(260));
        dot("$P$",H,dir(90));
        dot("$E$",EE,unit(EE-A));
        dot("$F$",F,unit(F-C));
        dot("$Q$",Q,NW);
        dot("$R$",R,N);
    \end{asy}
\end{center}
By construction line $EF$ is the Steiner line from $D$ to $\triangle ABC$; combining this with $\seg{AC}\perp\seg{BD}$ yields that $P$ is the orthocenter of $\triangle ABC$. Redefine $Q$ and $R$ as the reflections of $P$ over $\seg{BA}$ and $\seg{BC}$ respectively. It is well-known that $Q$ and $R$ lie on $\omega$. By properties of reflection $DEQP$ is an isosceles trapezoid and therefore cyclic, so $Q$ and $R$ are the points described in the problem. Finally $EQ=DP=FR$, and we are done.
