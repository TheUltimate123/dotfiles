% Input your problem and solution below.
% Three dashes on a newline indicate the breaking points.
% vim: tw=72

---

Let $ABC$ be an acute triangle with circumcircle $\Gamma$. Let $\ell$ be a tangent line to $\Gamma$, and let $\ell_a$, $\ell_b$, and $\ell_c$ be the lines obtained by reflecting $\ell$ in the lines $BC$, $CA$, and $AB$, respectively. Show that the circumcircle of the triangle determined by the lines $\ell_a$, $\ell_b$, and $\ell_c$ is tangent to the circle $\Gamma$.

---

Let $\ell$ touch $\Gamma$ at $X$. Make the following definitions with cyclic variations defined similarly as well: Denote $A_1=\ell_b\cap\ell_c$ and $A'=\ell\cap\ell_a$. Let $X_A$ be the reflection of $X$ over $\overline{BC}$ and $Y_A=\overline{BX_C}\cap\overline{CX_B}$.
\begin{center}
    \begin{asy}
        size(12cm);
        defaultpen(fontsize(9pt));

        pen pri=springgreen;
        pen sec=deepgreen;
        pen tri=chartreuse;
        pen fil=springgreen+opacity(0.05);
        pen sfil=deepgreen+opacity(0.05);
        pen tfil=chartreuse+opacity(0.05);

        real ttt=290;
        pair O, A, B, C, X, Ap, Bp, Cp, XA, XB, XC, A1, B1, C1, I, YA, YB, YC, T;
        O=(0, 0);
        A=dir(120);
        B=dir(215);
        C=dir(325);
        X=dir(ttt);
        Ap=extension(B, C, X, X+dir(ttt-90));
        Bp=extension(C, A, X, X+dir(ttt-90));
        Cp=extension(A, B, X, X+dir(ttt-90));
        XA=2*foot(X, B, C)-X;
        XB=2*foot(X, C, A)-X;
        XC=2*foot(X, A, B)-X;
        A1=extension(Bp, XB, Cp, XC);
        B1=extension(Cp, XC, Ap, XA);
        C1=extension(Ap, XA, Bp, XB);
        I=incenter(A1, B1, C1);
        YA=extension(B, XC, C, XB);
        YB=extension(C, XA, A, XC);
        YC=extension(A, XB, B, XA);
        T=extension(B1, YB, C1, YC);

        filldraw(circumcircle(A, B, C), fil, pri);
        filldraw(circumcircle(XA, B, C), fil, pri);
        filldraw(circumcircle(XB, C, A), fil, pri);
        filldraw(circumcircle(XC, A, B), fil, pri);
        filldraw(circumcircle(A1, B1, C1), sfil, sec);
        draw(A -- Cp, pri);
        draw(B -- Ap, pri);
        draw(Bp -- A, pri);
        draw(Ap -- Cp, sec);
        draw(Ap -- B1 -- A1 -- XB, sec); 
        draw(A1 -- A, sec);
        draw(B1 -- I -- C1, sec);
        draw(XB -- A -- XC, tri);
        draw(XC -- YA, tri);
        draw(B -- YC, tri);
        draw(YA -- XB, tri);
        draw(YB -- C, tri);
        draw(B1 -- T -- C1, tri);
        draw(T -- A1, tri);

        dot("$A$", A, N);
        dot("$B$", B, SW);
        dot("$C$", C, S);
        dot("$X$", X, X);
        dot("$A'$", Ap, E);
        dot("$B'$", Bp, SE);
        dot("$C'$", Cp, SW);
        dot("$X_A$", XA, N);
        dot("$X_B$", XB, SE);
        dot("$X_C$", XC, SW);
        dot("$A_1$", A1, S);
        dot("$B_1$", B1, NW);
        dot("$C_1$", C1, dir(15));
        dot("$I$", I, SW);
        dot("$Y_A$", YA, YA);
        dot("$Y_B$", YB, dir(110));
        dot("$Y_C$", YC, SW);
        dot("$T$", T, T);
    \end{asy}
\end{center}
Since \[\measuredangle AB'C'=\measuredangle AB'X=-\measuredangle AB'X_B=-\measuredangle AB'A_1,\]
and similarly $\measuredangle B'C'A=-\measuredangle A_1C'A$, $A$ is either the incenter of $A_1$-excenter of $\triangle A_1B'C'$. Thus, $\overline{A_1A}$ bisects $\angle C_1AB_1$, so by symmetry, $\overline{A_1A},\overline{B_1B},\overline{C_1C}$ concur at the incenter $I$ of $\triangle A_1B_1C_1$. Now, remark that \[\measuredangle BAC=90^\circ-\measuredangle B'A_1I=\measuredangle B_1IC=\measuredangle BIC,\]
whence $I\in(ABC)$. Since $\ell$ is tangent to $(ABC)$, $\ell_a$ is tangent to $(X_ABC)$, so \[\measuredangle ABY_A=\measuredangle ABX_C=\measuredangle XBA=\measuredangle XCA=\measuredangle ACX_B=\measuredangle ACY_A,\]
so $Y_A\in(ABC)$. By symmetry, $Y_B,Y_C\in(ABC)$ as well. Then, \[\measuredangle Y_BY_CB=\measuredangle Y_BCB=\measuredangle X_ACB=\measuredangle B_1X_AB,\]
so $\overline{Y_BY_C}\parallel\ell_a$, from which by symmetry, $\triangle A_1B_1C_1$ and $\triangle Y_AY_BY_C$ are homothetic. However, since $B_1,C_1,X_A$ are collinear, by the converse of Pascal's Theorem on $Y_BTY_CBIC$, $T\in\Gamma$, so $\Gamma$ and $(A_1B_1C_1)$ are tangent at $T$, as desired. 

