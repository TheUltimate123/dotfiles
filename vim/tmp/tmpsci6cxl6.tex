% Input your problem and solution below.
% Three dashes on a newline indicate the breaking points.
% vim: tw=72

---

Let $ABC$ be a scalene triangle with circumcircle $\Gamma$, and let $M$ be the midpoint of $\seg{BC}$. A variable point $P$ is selected on $\seg{AM}$. The circumcircles of triangles $BPM$ and $CPM$ intersect $\Gamma$ again at points $D$ and $E$, respectively. Lines $DP$ and $EP$ intersect the circumcircles of triangles $CPM$ and $BPM$ again at $X$ and $Y$, respectively. Prove that as  $P$ varies, the circumcircle of $\triangle AXY$ passes through a fixed point $T$ distinct from $A$.

---

\begin{center}
    \begin{asy}
        size(12cm);
        defaultpen(fontsize(10pt));

        pen pri=royalblue;
        pen sec=deepgreen;
        pen tri=deepcyan;
        pen fil=pri+opacity(0.05);
        pen sfil=sec+opacity(0.05);
        pen tfil=tri+opacity(0.05);

        pair O, A, B, C, M, P, D, EE, X, Y, Bp, Cp, SS, T, Q, R;
        O=(0,0);
        A=dir(110);
        B=dir(215);
        C=dir(325);
        M=(B+C)/2;
        P=(A+4M)/5;
        D=intersectionpoints(circle(O,1),circumcircle(B,P,M))[1];
        EE=intersectionpoints(circle(O,1),circumcircle(C,P,M))[0];
        X=2*foot(circumcenter(C,P,M),D,P)-P;
        Y=2*foot(circumcenter(B,P,M),EE,P)-P;
        Bp=2*foot(O,B,B+A-M)-B;
        Cp=2*foot(O,C,C+A-M)-C;
        SS=extension(B,C,Bp,Cp);
        T=2*foot(O,A,SS)-A;
        Q=extension(B,D,C,EE);
        R=extension(B,Y,C,X);

        filldraw(circle(O,1),fil,pri);
        filldraw(circumcircle(B,P,M),sfil,sec);
        filldraw(circumcircle(C,P,M),sfil,sec);
        filldraw(circumcircle(A,X,Y),tfil,tri);
        fill(A--B--C--cycle,fil);
        fill(B--Q--C--R--cycle,tfil);
        draw(A--B--C--A--Q,pri);
        draw(B--Bp,pri);
        draw(C--Cp,pri);
        draw(D--Cp,sec);
        draw(EE--Bp,sec);
        draw(A--SS--B,pri);
        draw(Cp--SS--X,tri);
        draw(B--Q--C--R--B,tri);

        dot("$A$",A,N);
        dot("$B$",B,SW);
        dot("$C$",C,dir(330));
        dot("$D$",D,dir(240));
        dot("$E$",EE,dir(300));
        dot("$P$",P,dir(200));
        dot("$M$",M,SW);
        dot("$B'$",Bp,dir(120));
        dot("$C'$",Cp,Cp);
        dot("$X$",X,N);
        dot("$Y$",Y,N);
        dot("$S$",SS,W);
        dot("$T$",T,NW);
        dot("$Q$",Q,S);
        dot("$R$",R,W);
    \end{asy}
\end{center}
Let $B'$ and $C'$ lie on $\Gamma$ with $\seg{AM}\parallel\seg{BB'}\parallel\seg{CC'}$. Denote $S=\seg{BC}\cap\seg{B'C'}$, and let $\seg{AS}$ intersect $\Gamma$ again at $T$. I claim that $T$ is the fixed point.
\setcounter{iclaim}0
\begin{iclaim}
    $BCXY$ is cyclic.
\end{iclaim}
\begin{proof}
    By the Radical Axis Theorem, on $\Gamma$, $(BPM)$, and $(CPM)$, $\seg{AM}$, $\seg{BD}$, $\seg{CE}$ concur at a point $Q$. Let $R$ be the reflection of $Q$ over $M$, so that $BQCR$ is a parallelogram. By Reim's Theorem, $\seg{BY}\parallel\seg{CE}$, so $Y$ lies on $\seg{BR}$. Similarly $X$ lies on $\seg{CR}$, so $\seg{AM}$, $\seg{BY}$, $\seg{CX}$ concur at $R$.

    Now it is easy to check that $RB\cdot RY=RP\cdot RM=RC\cdot RX$, as desired.
\end{proof}
\begin{iclaim}
    $B'C'XY$ is cyclic.
\end{iclaim}
\begin{proof}
    First note that \[\da B'PM=\da PB'B=\da EB'B=\da ECB=\da ECM=\da EPM,\]
    whence $B'$ lies on $\seg{PE}$. Similarly, $C'$ lies on $\seg{PD}$.

    Since $\da CXY=\da CBY=\da BCE=\da BDE$ and $\seg{CX}\parallel\seg{BD}$, it follows that $\seg{XY}\parallel\seg{DE}$, whence $B'C'XY$ is cyclic by Reim's Theorem.
\end{proof}

By the Radical Axis Theorem on $\Gamma$, $(BCXY)$, and $(B'C'XY)$, $S$ lies on $\seg{XY}$. To finish, just note that $SA\cdot ST=SB\cdot SC=SX\cdot SY$, whence $T$ lies on $(AXY)$. Since $T$ is fixed, we are done.

