% Input your problem and solution below.
% Three dashes on a newline indicate the breaking points.

---

Let $ABCD$ be a cyclic quadrilateral, and let $M$, $N$ be the midpoints of $\seg{AC}$, $\seg{BD}$. The circumcircles of $\triangle ABM$, $\triangle CDM$ intersect again at $P$, the circumcircles of $\triangle ABN$, $\triangle CDN$ intersect again at $Q$, the circumcircles of $\triangle ADM$, $\triangle BCM$ intersect again at $R$, and the circumcircles of $\triangle ADN$, $\triangle BCM$ intersect again at $S$. Prove that $\seg{PQ}\parallel\seg{RS}$.

---

Let $O$ be the circumcenter and let $E=\seg{AC}\cap\seg{BD}$, $F=\seg{AB}\cap\seg{CD}$. In fact, we will show $\seg{OE}\perp\seg{PQ}$. By symmetry the desired result follows from here.
\begin{center}
\begin{asy}
    size(9cm); defaultpen(fontsize(10pt));
    pen pri=blue;
    pen sec=heavygreen;
    pen tri=red;
    pen fil=cyan+opacity(0.05);
    pen sfil=green+opacity(0.05);
    pen tfil=red+opacity(0.05);
    pair O,A,B,C,D,EE,F,M,NN,P,Q,J;
    O=(0,0);
    A=dir(140);
    B=dir(210);
    C=dir(330);
    D=dir(95);
    EE=extension(A,C,B,D);
    F=extension(A,B,C,D);
    M=(A+C)/2;
    NN=(B+D)/2;
    J=foot(O,EE,F);
    P=reflect(circumcenter(A,B,M),circumcenter(C,D,M))*M;
    Q=reflect(circumcenter(A,B,NN),circumcenter(C,D,NN))*NN;
    filldraw(circumcircle(O,M,EE),tfil,tri);
    draw(F--J,tri+Dotted);
    draw(Q--F--M,sec+dashed);
    filldraw(circumcircle(A,B,M),sfil,sec);
    filldraw(circumcircle(C,D,M),sfil,sec);
    draw(C--A--F--D--B,pri);
    filldraw(circle(O,1),fil,pri);
    filldraw(A--B--C--D--cycle,fil,pri);
    dot("$O$",O,dir(260));
    dot("$A$",A,NW);
    dot("$B$",B,SW);
    dot("$C$",C,dir(300));
    dot("$D$",D,NE);
    dot("$E$",EE,dir(95));
    dot("$F$",F,N);
    dot("$M$",M,dir(10));
    dot("$N$",NN,W);
    dot("$P$",P,NE);
    dot("$Q$",Q,SW);
    dot("$J$",J,S);
\end{asy}
\end{center}
First $O$, $M$, $N$, $E$ are concyclic; say their circumcircle is $\omega$. I contend:
\begin{claim*}
    $P$ lies on $\seg{FM}$ and $\omega$. Analogously $Q$ lies on $\seg{FN}$ and $\omega$.
\end{claim*}
\begin{proof}
    To see $P\in\seg{FM}$, apply Radical Axis theorem on $(ABM)$, $(CDM)$, $(ABCD)$.

    Let $J=(ABE)\cap(CDE)\setminus E$ be the Miquel point of $ABDC$. By properties of the Miquel point, $J$ is the foot from $O$ to $\seg{EF}$, whence $J\in\omega$. Then $FM\cdot FP=FA\cdot FB=FE\cdot FJ$, so $P\in\omega$.
\end{proof}

Finally $\da PME=\da FMA=-\da FND=-\da QNE$ by $\triangle FAC\sim\triangle FDB$, so $\widehat{EP}=\widehat{EQ}$ on $\omega$. Since $\seg{OE}$ is a diameter of $\omega$, we have $\seg{OE}\perp\seg{PQ}$, and we are done.

